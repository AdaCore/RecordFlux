package Test is

   type Length is range 1 .. 2 ** 14 - 1 with Size => 16;

   type Message (Length : Length; Extended : Boolean) is
      message
         Data : Opaque
            with Size => Length * 8
            then Extension
               if Extended = True
            then null
               if Extended = False;
         Extension : Opaque
            with Size => Length * 8;
      end message;

   generic
      C : Channel with Readable, Writable;
   machine S is
      M_R : Message;
      M_S : Message;
      Extended : Boolean := False;
   begin
      state Start
      is
      begin
         M_R'Reset (Length => 2, Extended => Extended);
      transition
         goto Receive
      end Start;

      state Receive
      is
      begin
         C'Read (M_R);
      transition
         goto Process
            if M_R'Valid
         goto Error
      exception
         goto null -- Â§S-S-E
      end Receive;

      state Process
      is
         Length : Length;
         M_T : Message;
         Equal : Boolean;
      begin
         M_S := Message'(Length => M_R.Length, Extended => True, Data => M_R.Data, Extension => [3, 4]);
         Length := M_S.Length;
         M_T'Reset (Length => M_S.Length, Extended => True);
         M_T.Data := M_R.Data;
         M_T.Extension := [3, 4];
         Equal := M_S.Length = M_T.Length and M_S.Extended = M_T.Extended;
      transition
         goto Reply
            if Length = M_R.Length and Equal
         goto Error
      exception
         goto Error
      end Process;

      state Reply
      is
      begin
         C'Write (M_S);
      transition
         goto Reset
      end Reply;

      state Reset
      is
      begin
         M_S'Reset (Length => M_R.Length, Extended => M_R.Extended);
      transition
         goto null
      end Reset;

      state Error
      is
      begin
      transition
         goto null
      end Error;
   end S;

end Test;
