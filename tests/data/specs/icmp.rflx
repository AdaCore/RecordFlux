package ICMP is

   type Tag is (
      Echo_Reply => 0,
      Echo_Request => 8,
      Destination_Unreachable => 3,
      Time_Exceeded => 11,
      Parameter_Problem => 12,
      Source_Quench => 4,
      Redirect => 5,
      Timestamp_Msg => 13,
      Timestamp_Reply => 14,
      Information_Request => 15,
      Information_Reply => 16
   ) with Size => 8;

   type Code_Destination_Unreachable is (
      Net_Unreachable => 0,
      Host_Unreachable => 1,
      Protocol_Unreachable => 2,
      Port_Unreachable => 3,
      Fragmentation_Needed_DF_Set => 4,
      Source_Route_Failed => 5
   ) with Size => 8;

   type Code_Time_Exceeded is (
      TTL_Exceeded => 0,
      Fragment_Reassembly_Time_Exceeded => 1
   ) with Size => 8;

   type Code_Redirect is (
      Redirect_for_Network => 0,
      Redirect_for_Host => 1,
      Redirect_for_Service_Network => 2,
      Redirect_for_Service_Host => 3
   ) with Size => 8;

   type Code_Zero is range 0 .. 0 with Size => 8;
   type Checksum is range 0 .. 2 ** 16 - 1 with Size => 16;
   type Identifier is range 0 .. 2 ** 16 - 1 with Size => 16;
   type Sequence_Number is range 0 .. 2 ** 16 - 1 with Size => 16;
   type Pointer is range 0 .. 2 ** 8 - 1 with Size => 8;
   type Timestamp is range 0 .. 2 ** 32 - 1 with Size => 32;
   type Gateway_Internet_Address is range 0 .. 2 ** 32 - 1 with Size => 32;
   type Unused_32 is range 0 .. 0 with Size => 32;
   type Unused_24 is range 0 .. 0 with Size => 24;

   type Message is
      message
         Tag : Tag
            then Code_Destination_Unreachable
               if Tag = Destination_Unreachable
            then Code_Time_Exceeded
               if Tag = Time_Exceeded
            then Code_Redirect
               if Tag = Redirect
            then Code_Zero
               if (Tag = Echo_Reply
                   or Tag = Echo_Request
                   or Tag = Source_Quench
                   or Tag = Parameter_Problem
                   or Tag = Timestamp_Msg
                   or Tag = Timestamp_Reply
                   or Tag = Information_Request
                   or Tag = Information_Reply);
         Code_Destination_Unreachable : Code_Destination_Unreachable
            then Checksum;
         Code_Time_Exceeded : Code_Time_Exceeded
            then Checksum;
         Code_Redirect : Code_Redirect
            then Checksum;
         Code_Zero : Code_Zero;
         Checksum : Checksum
            then Unused_32
               if (Tag = Destination_Unreachable
                   or Tag = Source_Quench
                   or Tag = Time_Exceeded)
            then Identifier
               if (Tag = Echo_Request
                   or Tag = Echo_Reply
                   or Tag = Timestamp_Msg
                   or Tag = Timestamp_Reply
                   or Tag = Information_Request
                   or Tag = Information_Reply)
            then Pointer
               if Tag = Parameter_Problem
            then Gateway_Internet_Address
               if Tag = Redirect;
         Gateway_Internet_Address : Gateway_Internet_Address
            then Data
               with Size => 224;
         Pointer : Pointer;
         Unused_24 : Unused_24
            then Data
               with Size => 224;
         Unused_32 : Unused_32
            then Data
               with Size => 224;
         Identifier : Identifier;
         Sequence_Number : Sequence_Number
            then Originate_Timestamp
               if Tag = Timestamp_Msg or Tag = Timestamp_Reply
            then null
               if Tag = Information_Request or Tag = Information_Reply
            then Data
               with Size => Message'Last - Sequence_Number'Last
               if Tag = Echo_Reply or Tag = Echo_Request;
         Originate_Timestamp : Timestamp;
         Receive_Timestamp : Timestamp;
         Transmit_Timestamp : Timestamp
            then null;
         Data : Opaque;
      end message;

end ICMP;
